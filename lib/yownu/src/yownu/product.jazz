;;;=========
;;;  Yownu
;;;=========
;;;
;;;; Yownu Product
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module yownu.product jazz


(import (jazz.io)
        (jazz.product))


;;;
;;;; Build
;;;


(definition (build descriptor (unit: unit #f) (skip-references?: skip-references? #f) (force?: force? #f))
  (define (resources-dir)
    (let ((bundle (product-descriptor-build-bundle descriptor)))
      (if bundle
          (new-directory {Directory Build} (format "{a}.app/Contents/Resources" bundle))
        {Directory Build})))
  
  (let ((project-dir {Directory Yownu})
        (resources-dir (resources-dir)))
    (define (install-filename path)
      (let ((src (new-file project-dir path))
            (dst (new-file resources-dir path)))
        (install-file src dst
          feedback: (lambda (action pathname)
                      (build-feedback "; {a} {a}..." action (get-name pathname))))))
    
    (define (install-dirname path (src #f) (dst #f))
      (let ((src (new-directory project-dir (or src path)))
            (dst (new-directory resources-dir (or dst path))))
        (install-directory src dst
          feedback: (let ((first? #t))
                      (lambda (action pathname)
                        (when first?
                          (build-feedback "; {a} {a}..." action (get-name pathname))
                          (set! first? #f)))))))
    
    (define (copy-info)
      (install-filename '("README"))
      (install-filename '("devel" "yownu" "About.ftx"))
      (install-filename '("devel" "yownu" "Help.ftx"))
      (install-filename '("devel" "yownu" "News.ftx") )
      (install-filename '("devel" "yownu" "Roadmap.ftx")))
    
    (define (copy-assets)
      (install-dirname '("world" "lib" "world.client" "assets"))
      (install-dirname '("lib" "yownu" "assets")))
    
    (define (copy-doc)
      (install-dirname '("world" "lib" "world.website" "content")))
    
    (define (copy-external)
      (when (eq? kernel-platform 'windows)
        (install-filename '("devel" "external" "fbx-conv" "windows" "fbx-conv.exe"))))
    
    (define (copy-scripts)
      (install-dirname '("devel" "scripts")))
    
    (define (copy-templates)
      (install-dirname '("devel" "templates")))
    
    (build-product-descriptor descriptor unit: unit skip-references?: skip-references? force?: force?)
    (initialize-aliases)
    (copy-info)
    (copy-assets)
    (copy-doc)
    (copy-external)
    (copy-scripts)
    (copy-templates)))


;;;
;;;; Install
;;;


(definition install-dir
  (cond-expand
    (windows
     {Directory Home "yownu" "install"})
    (else
     {Directory Home "Devel" "yownu" "install"})))


(definition install-name
  (cond-expand
    (mac
     (cond-expand (debug
                   "yownu-debug.app")
                  (else
                   "yownu.app")))
    (unix
     (cond-expand (debug
                   "Yownud")
                  (else
                   "Yownu")))
    (else
     (cond-expand (debug
                   "yownud")
                  (else
                   "yownu")))))


(definition (install descriptor)
  (initialize-aliases)
  ;; appl
  (install-application descriptor install-dir
    name: install-name
    scriptable?: #t
    platform?: #t
    opengl?: #t
    git?: #t
    gstreamer?: #t
    files: (cond-expand
             (windows '("README" "yownu-server.exe" "yownu-processor.exe" "yownu-worker.exe"))
             (unix '("README" "Yownu-Server" "Yownu-Processor" "Yownu-Worker"))
             (else '("README")))
    directories: '("devel")
    ignored-macosx: '("yownu-server.app"
                      "yownu-processor.app"
                      "yownu-worker.app"))
  ;; apps
  (cond-expand
    (mac
     (let ((app-dir (new-directory install-dir install-name)))
       (install-directory {Directory Build "yownu-server.app"} (new-directory app-dir '("Contents" "MacOS" "yownu-server.app"))
         feedback: (lambda (action pathname)
                     (build-feedback "; {a} {a}..." action (get-name pathname))))
       (install-directory {Directory Build "yownu-processor.app"} (new-directory app-dir '("Contents" "MacOS" "yownu-processor.app"))
         feedback: (lambda (action pathname)
                     (build-feedback "; {a} {a}..." action (get-name pathname))))
       (install-directory {Directory Build "yownu-worker.app"} (new-directory app-dir '("Contents" "MacOS" "yownu-worker.app"))
         feedback: (lambda (action pathname)
                     (build-feedback "; {a} {a}..." action (get-name pathname))))))
    (else)))


;;;
;;;; Deploy
;;;


(definition (deploy descriptor)
  (initialize-aliases)
  ;; appl
  (deploy-application descriptor
                      (new-directory install-dir install-name)
                      make: 'yownu-all
                      install: 'yownu))


;;;
;;;; Register
;;;


(register-product 'yownu
  build: build
  install: install
  deploy: deploy))
