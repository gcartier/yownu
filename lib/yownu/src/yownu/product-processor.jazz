;;;=========
;;;  Yownu
;;;=========
;;;
;;;; Yownu-Processor Product
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module yownu.product-processor jazz


(import (jazz.io)
        (jazz.product))


;;;
;;;; Build
;;;


(definition (build descriptor (unit: unit #f) (force?: force? #f))
  (define (resources-dir)
    (let ((bundle (product-descriptor-build-bundle descriptor)))
      (if bundle
          (new-directory~ {Directory Build} (format "{a}.app/Contents/Resources" bundle))
        {Directory Build})))
  
  (let ((project-dir {Directory Yownu})
        (resources-dir (resources-dir)))
    (define (install-file path)
      (let ((src (new-file~ project-dir path))
            (dst (new-file~ resources-dir path)))
        (install-file~ src dst
          feedback: (lambda (action pathname)
                      (build-feedback "; {a} {a}..." action (get-name~ pathname))))))
    
    (define (install-directory path (src #f) (dst #f))
      (let ((src (new-directory~ project-dir (or src path)))
            (dst (new-directory~ resources-dir (or dst path))))
        (install-directory~ src dst
          feedback: (let ((first? #t))
                      (lambda (action pathname)
                        (when first?
                          (build-feedback "; {a} {a}..." action (get-name~ pathname))
                          (set! first? #f)))))))
    
    (define (copy-info)
      (install-file '("README"))
      (install-file '("devel" "yownu" "About.ftx"))
      (install-file '("devel" "yownu" "Help.ftx"))
      (install-file '("devel" "yownu" "News.ftx") )
      (install-file '("devel" "yownu" "Roadmap.ftx")))
    
    (define (copy-assets)
      (install-directory '("world" "lib" "world.client" "assets"))
      (install-directory '("lib" "yownu" "assets")))
    
    (define (copy-templates)
      (install-directory '("devel" "templates")))
    
    (build-product-descriptor descriptor unit: unit force?: force?)
    (initialize-aliases)
    (copy-info)
    (copy-assets)
    (copy-templates)))


;;;
;;;; Install
;;;


(cond-expand
  (mac
    (definition install-dir
      {Directory Home "Devel" "yownu-processor" "install"}))
  (else
    (definition install-dir
      {Directory Home "yownu-processor" "install"})))


(definition (install descriptor)
  (initialize-aliases)
  ;; appl
  (install-application descriptor install-dir
    scriptable?: #t
    platform?: #t
    opengl?: #t
    git?: #t
    audio?: #t
    files: (cond-expand
             (windows '("README" "yownu-worker.exe"))
             (else '("README")))
    directories: '("devel")))


;;;
;;;; Deploy
;;;


(definition installation-dirname
  (cond-expand
    (mac "yownu-processor.app")
    (else "yownu-processor")))


(definition (deploy descriptor)
  (initialize-aliases)
  ;; appl
  (deploy-application descriptor
                      (new-directory~ install-dir installation-dirname)
                      make: 'yownu-all
                      install: 'yownu))


;;;
;;;; Register
;;;


(register-product 'yownu-processor
  build: build
  install: install
  deploy: deploy))
