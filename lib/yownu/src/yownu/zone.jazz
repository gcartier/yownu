;;;=========
;;;  Yownu
;;;=========
;;;
;;;; Yownu Zone
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2014
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module yownu.zone jazz


(import (jazz.graphic.opengl.glew)
        (jazz.jml)
        (jazz.system)
        (world)
        (world.atlas)
        (world.autoload)
        (world.geometry)
        (world.planet)
        (world.skybox)
        (world.syntax (phase syntax))
        (world.zone)
        (yownu.biome))


(class Yownu-Zone extends Zone
  
  
  (form
    (<install> floor-level: #f))
  
  
  ;;;
  ;;;; Populate
  ;;;
  
  
  (method override (populate-zone)
    (let ((space-radius (find-setting 'yownu.radius 250.))
          (space-density (find-setting 'yownu.density 5)))
      (define (place-planets)
        (loop (repeat space-density)
              (let ((position (random-space))
                    (size (random-between (find-setting 'yownu.planet-min-size 5.) (find-setting 'yownu.planet-max-size 50.)))
                    (biome (random-biome)))
                (let ((image (get-earth~ biome)))
                  (let ((planet (new Planet parent: self position: position size: size image: image)))
                    (generate-planet~ planet)
                    (add-element planet)
                    (let ((trees (get-trees~ biome)))
                      (when trees
                        (populate-trees planet trees))))))))
      
      (define (random-space)
        (vertex (random-between (- space-radius) space-radius)
                (random-between (- space-radius) space-radius)
                (random-between (- space-radius) space-radius)))
      
      (site (populate on?: #f)
        (place-planets))
      (place-player)))
  
  
  (method (populate-trees planet trees)
    (let ((world (current-world)))
      (let ((size (get-size~ planet)))
        (loop (repeat (random (fxround (* (find-setting 'yownu.planet-trees 10.) size))))
              (let ((model (random-element trees)))
                (receive (dir pos) (random-position planet)
                  (let ((entity (place-model~ world model pos savable?: #f)))
                    (realign-lookat~ entity dir))))))))
  
  
  (method (place-player)
    (let ((me (current-me))
          (planets (collect-planets)))
      (unless (null? planets)
        (let ((planet (random-element planets)))
          (receive (dir pos) (random-position planet 25.)
            (set-position~ me pos)
            (realign-lookat~ me dir))))))
  
  
  (method (random-position planet (surface-distance 0.))
    (let ((world (current-world)))
      (let ((position (get-position~ planet))
            (radius (get-radius~ planet))
            (dir (random-direction)))
        (let ((pos (vertex+ position (vertex-scalar* dir (+ radius surface-distance)))))
          (values dir pos)))))
  
  
  ;;;
  ;;;; Blocks
  ;;;
  
  
  (method override (create-atlas)
    (let ((world (current-world)))
      (if (not (get-earth?~ world))
          (nextmethod)
        (or (registered-atlas 'yownu)
            (let ((atlas (create-yownu-atlas)))
              (register-atlas 'yownu atlas)
              atlas)))))
  
  
  (method override (create-atlas-nearest)
    (let ((world (current-world)))
      (if (not (get-earth?~ world))
          (nextmethod)
        (or (registered-atlas 'yownu-nearest)
            (let ((atlas (create-yownu-atlas min-filter: GL_NEAREST)))
              (register-atlas 'yownu-nearest atlas)
              atlas)))))
  
  
  (method (create-yownu-atlas (min-filter: min-filter #f) (mag-filter: mag-filter #f))
    (let ((world (current-world)))
      (make-block-atlas~ world
        min-filter: min-filter
        mag-filter: mag-filter
        adder: (lambda (atlas)
                 (add-file~ atlas (find-texture-file~ world "earth"))))))
  
  
  ;;;
  ;;;; Skybox
  ;;;
  
  
  (method override (prepare-skybox)
    (install-skybox (find-skybox "nebula")))
  
  
  ;;;
  ;;;; Edition
  ;;;
  
  
  (method override (zone-editable?)
    #f)))
